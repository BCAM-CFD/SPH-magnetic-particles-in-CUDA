/******************************************************
This code has been developed by Adolfo Vazquez-Quesada,
from the Department of Fundamental Physics at UNED, in
Madrid, Spain.
email: a.vazquez-quesada@fisfun.uned.es
********************************************************/

#include "kernel_functions.h"
#include "config.h"

//Part 1 of the function to move particles with a velocity Verlet method with lambda = 0.5.
__global__ void kernel_move_sys_VV_part1(real* __restrict__ x,
					 real* __restrict__ y,
					 real* __restrict__ z,
					 real* __restrict__ vx,
					 real* __restrict__ vy,
					 real* __restrict__ vz,				
					 real* __restrict__ fx,
					 real* __restrict__ fy,
					 real* __restrict__ fz,
					 real* __restrict__ mass,
					 int* __restrict__ type) {
  int i = threadIdx.x + blockIdx.x * blockDim.x;
  if (i >= N) return;

  int type_i = type[i];
  if (type_i > 2) return; // particles of colloids are not moved here

  real xi  = x[i];
  real yi  = y[i];
  real zi;
  if (dim == 3)  
    zi  = z[i];

  if (type_i == 0) { // i fluid
    
    real half_dt_over_mass = 0.5 * dt / mass[i];
    // Velocity at t + dt/2 
    vx[i] = vx[i] + half_dt_over_mass * fx[i];
    vy[i] = vy[i] + half_dt_over_mass * fy[i];    
    if (dim == 3)
      vz[i] = vz[i] + half_dt_over_mass * fz[i];
  }
  else if (type_i == 1) {// i bottom wall
    
    vx[i] = V_bottom;
    vy[i] = 0.0;
    if (dim == 3)    
      vz[i] = 0.0;
  }
  else if (type_i == 2) {// i top wall
    vx[i] = V_top;
    vy[i] = 0.0;
    if (dim == 3)    
      vz[i] = 0.0;
  }  //Particles with type_i > 2 where discarded above

  // Position at t + dt
  xi = xi + vx[i] * dt;
  yi = yi + vy[i] * dt;
  if (dim == 3)
    zi = zi + vz[i] * dt;
  
  // Periodic Boundary conditions
  if (xi < 0)
    xi = xi + L[0];
  if (wall == 0) //--- If no wall ---
    if (yi < 0)
      yi = yi + L[1];
  if (dim == 3)
    if (zi < 0)
      zi = zi + L[2];
  
  if (xi > L[0])
    xi = xi - L[0];
  if (wall == 0) //--- If no wall ---    
    if (yi > L[1])
      yi = yi - L[1];
  if (dim == 3)
    if (zi > L[2])
      zi = zi - L[2];      

  // Data is stored  (v[i] is stored above, only when needed (when type_i == 0)  
  x[i] = xi;
  y[i] = yi;
  if (dim == 3)
    z[i] = zi;  
}
